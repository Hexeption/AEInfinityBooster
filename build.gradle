buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.1.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: 'maven-publish'

group = 'uk.co.hexeption'
version = '1.18.2-1.1.0'
def env = System.getenv()
if (env.GITHUB_RUN_NUMBER) {
    version = version + "+" + "${env.GITHUB_RUN_NUMBER}"
}

java {
    archivesBaseName = 'AEInfinityBooster'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: 'official', version: '1.18.2'

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                aeinfinitybooster {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                aeinfinitybooster {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'aeinfinitybooster', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                aeinfinitybooster {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "AEInfinityBooster.refmap.json"

    config "AEInfinityBooster.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
        content {
            includeGroup 'appeng'
        }
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.0.44'
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.2:9.5.5.174")
    compileOnly fg.deobf("appeng:appliedenergistics2:11.0.0-alpha.4")
    runtimeOnly fg.deobf("appeng:appliedenergistics2:11.0.0-alpha.4")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "aeinfinitybooster",
                //"Specification-Vendor": "aeinfinitybooster authors",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                //"Implementation-Vendor": "aeinfinitybooster authors",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "AEInfinityBooster.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

artifacts {
    archives jar
    archives sourcesJar
    archives deobfJar
}

curseforge {
    if (env.CURSE_API_KEY) {
        apiKey = env.CURSE_API_KEY
    }
    project {
        id = '559313'
        changelog = 'A changelog can be found at https://github.com/Hexeption/AEInfinityBooster'
        releaseType = 'release'
        addGameVersion '1.18'
        addGameVersion '1.18.2'

        mainArtifact(jar) {
            displayName = "AEInfinityBooster v$version"
        }
        relations {
            requiredDependency 'applied-energistics-2'
        }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact deobfJar
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
        if (env.REPO_PASSWORD) {
            maven {
                url = "https://nexus.hexeption.dev/repository/hex-mods/"
                credentials {
                    username = "admin"
                    password = env.REPO_PASSWORD
                }
            }
        }
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){
    if (env.MODRINTH_API_KEY) {
        token = env.MODRINTH_API_KEY
    }
    projectId = 'VQhDBNs8'
    versionName = "AEInfinityBooster v$version"
    changelog = 'A changelog can be found at https://github.com/Hexeption/AEInfinityBooster'
    versionNumber = "$version"
    uploadFile = jar
    addGameVersion("1.18.2")
    addLoader('forge')
}

